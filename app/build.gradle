buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath 'io.fabric.tools:gradle:1.+'
    }
}
apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

repositories {
    maven { url 'https://maven.fabric.io/public' }
}
def readVersion(){
    def versionFile=new File(project.rootDir,'version.properties')
    def version=new Properties()
    def stream
    try{
        stream=new FileInputStream(versionFile)
        version.load(stream)
    }catch (FileNotFoundException i){}
    finally {
        if(stream!=null)
            stream.close()
    }
    if(!version['major'])version['major']="1"
    if(!version['minor'])version['minor']="0"
    if(!version['rel'])version['rel']="0"
    if(!version['build'])version['build']="0"
    return version

}
def readVersionName(){
    def version=readVersion()
    return "${version['major']}.${version['minor']}.${version['rel']}"
}
def readBuildCode(){
    def version=readVersion()
    def build=version['build'] as int
    return build
}

def incrementBuildNumber(){
    def versionFile=new File(project.rootDir,'version.properties')
    def version=readVersion()
    def build =version['build'] as int
    build++
    version['build']= build.toString();
    def stream = new FileOutputStream(versionFile)
    try{
        version.store(stream,null)
    }catch (FileNotFoundException i){}
    finally {
        if(stream!=null)
            stream.close()
    }
    println "El numero de build es "+build
    return build

}


def incrementVersionNumber(def type="minor") {
    def versionFile = new File(project.rootDir, 'version.properties')
    def version = readVersion()

    def major = version['major'] as int
    def minor = version['minor'] as int
    def rel = version['rel'] as int
    if (type == 'major') {
        major++
        minor = 0
        rel = 0
    } else if (type == 'minor') {
        minor++;
        rel = 0
    }else{
        rel++;
    }
    version['major'] =major
    version['minor'] =minor
    version['rel'] =rel
    def stream = new FileOutputStream(versionFile)
    try{
        version.store(stream,null)
    }catch (FileNotFoundException i){}
    finally {
        if(stream!=null)
            stream.close()
    }
    println "la version es "+major+"."+minor+"."+rel
    return major+"."+minor+"."+rel
}
task doMajorVersionIncrement<<{
    println "Incrementando major version"
    incrementVersionNumber('major')
}
task doMinorVersionIncrement<<{
    println "Incrementando minor version"
    incrementVersionNumber('minor')
}
task doRelVersionIncrement<<{
    println "Incrementando rel version"
    incrementVersionNumber('rel')
}

task doIncrementBuild<<{
    println "Incrementando el build"
    incrementBuildNumber()
}

android {
    compileSdkVersion 22
    buildToolsVersion '25.0.0'

    defaultConfig {
        applicationId "telecom.televisa.com.izzi"
        minSdkVersion 16
        targetSdkVersion 16
        versionCode readBuildCode()
        versionName readVersionName()
    }
    signingConfigs {
        release {
            storeFile file(RELEASE_STORE_FILE)
            storePassword RELEASE_STORE_PASSWORD
            keyAlias RELEASE_KEY_ALIAS
            keyPassword RELEASE_KEY_PASSWORD
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    lintOptions {
        abortOnError false
    }
    repositories {
        mavenCentral()
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
        // maven {
        //   credentials{
        //
        //}
        //url "https://github.com/jwmsolutions/izziConnectAndroid/"
        //}
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')

    compile 'com.android.support:support-v4:22.2.0'
    compile 'com.makeramen:roundedimageview:2.0.1'
    compile 'com.google.code.gson:gson:2.5'
    compile 'io.card:android-sdk:5.2.0'
    compile 'com.michaelpardo:activeandroid:3.1.0-SNAPSHOT'
    compile 'com.facebook.android:facebook-android-sdk:4.7.0'
    compile 'com.google.zxing:core:2.2'
    compile 'net.hockeyapp.android:HockeySDK:3.5.0'
    compile 'com.squareup.picasso:picasso:2.3.2'
    compile 'com.nineoldandroids:library:2.4.0'
    compile 'com.daimajia.slider:library:1.1.5@aar'
    compile 'com.google.android.gms:play-services-gcm:7.5.0'
    compile 'pl.droidsonroids.gif:android-gif-drawable:1.1.11'
    compile 'com.github.dbachelder:CreditCardEntry:1.4.7'
    compile 'com.hudomju:swipe-to-dismiss-undo:1.0'
    compile 'com.android.support.constraint:constraint-layout:1.0.2'
    compile 'com.android.support:appcompat-v7:22.2.1'
    compile('com.crashlytics.sdk.android:crashlytics:2.8.0@aar') {
        transitive = true;
    }
    compile('com.crashlytics.sdk.android:answers:1.4.1@aar') {
        transitive = true;
    }

}
